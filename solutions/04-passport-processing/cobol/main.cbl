IDENTIFICATION DIVISION.
	PROGRAM-ID. AOC-2020-DAY-00.

ENVIRONMENT DIVISION.
	INPUT-OUTPUT SECTION.
	FILE-CONTROL.
		SELECT InputFile ASSIGN TO "input.txt"
			ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
	FILE SECTION.
	FD InputFile.
	01 INPUT-FILE.
		05 INPUT-LINE PIC X(80).
	
	WORKING-STORAGE SECTION.
	01 WS-EOF PIC 9(1) VALUE 0.
	01 WS-INPUT.
		05 WS-INPUT-LINE OCCURS 1300 TIMES INDEXED BY I.
			10 WS-PASSPORT-LINE PIC X(80).
		05 WS-LINE-COUNT PIC 9(8).
	01 WS-CURRENT-LINE PIC 9(9) VALUE 1.
	01 WS-PASSPORT-STRING PIC X(100).
	01 WS-STRING-LENGTH PIC 9(8).
	01 WS-LINE-LENGTH PIC 9(8).
	01 WS-FIELDS.
		05 WS-FIELD OCCURS 8 TIMES INDEXED BY J.
			10 WS-FIELD-VALUE PIC X(15).
		05 WS-FIELD-COUNT PIC 9(4).
	01 WS-TALLY PIC 9(4).
	01 WS-REQUIRED-COUNT PIC 9(4).
	01 WS-REQUIRED-FIELDS PIC A(30) VALUE "byr iyr eyr hgt hcl ecl pid".
	01 WS-VALID-EYES      PIC A(30) VALUE "amb blu brn gry grn hzl oth".
	01 WS-CURRENT-FIELD.
		05 WS-CURRENT-KEY PIC A(3).
		05 WS-COLON PIC A(1).
		05 WS-CURRENT-VALUE PIC X(15).
	01 WS-PASSPORT-FIELDS-VALID PIC 9(1).
	01 WS-PASSPORT-DATA-VALID PIC 9(1).
	01 WS-ALL-FIELDS-COUNT PIC 9(8) VALUE 0.
	01 WS-VALID-DATA-COUNT PIC 9(8) VALUE 0.
	01 WS-FORMATTED-NUMBER PIC ZZZZ.

PROCEDURE DIVISION.
	
	OPEN INPUT InputFile.
		PERFORM VARYING I FROM 1 BY 1 UNTIL WS-EOF=1
			READ InputFile INTO WS-INPUT-LINE(I)
				AT END
					SET I TO 1
					MOVE 1 TO WS-EOF
				NOT AT END
					MOVE I TO WS-LINE-COUNT
			END-READ
		END-PERFORM.
	CLOSE InputFile.
	
	PERFORM UNTIL I>WS-LINE-COUNT
		PERFORM READ-PASS-STRING-PARA
		PERFORM FILL-PASS-FIELDS-PARA
		PERFORM CHECK-REQUIRED-FIELDS-PARA
		IF WS-PASSPORT-FIELDS-VALID=1
			ADD 1 TO WS-ALL-FIELDS-COUNT
			MOVE 1 TO WS-PASSPORT-DATA-VALID
			PERFORM VALIDATE-DATA-PARA VARYING J FROM 1 BY 1 UNTIL J>WS-FIELD-COUNT
			IF WS-PASSPORT-DATA-VALID=1
				ADD 1 TO WS-VALID-DATA-COUNT
			END-IF
		END-IF
	END-PERFORM.
	
	MOVE WS-ALL-FIELDS-COUNT TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 1 answer: " WS-FORMATTED-NUMBER.
	MOVE WS-VALID-DATA-COUNT TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 2 answer: " WS-FORMATTED-NUMBER.
	
	STOP RUN.
	
	READ-PASS-STRING-PARA.
	MOVE WS-INPUT-LINE(I) TO WS-PASSPORT-STRING.
	SET I UP BY 1.
	PERFORM VARYING I FROM I BY 1 UNTIL WS-INPUT-LINE(I)=SPACES
		STRING
			FUNCTION TRIM(WS-PASSPORT-STRING); SPACE;
			FUNCTION TRIM(WS-INPUT-LINE(I));
			INTO WS-PASSPORT-STRING
		END-STRING
	END-PERFORM.
	SET I UP BY 1.
	
	FILL-PASS-FIELDS-PARA.
	PERFORM VARYING J FROM 1 BY 1 UNTIL J>8
		MOVE SPACES TO WS-FIELD(J)
	END-PERFORM.
	MOVE 0 TO WS-FIELD-COUNT.
	UNSTRING WS-PASSPORT-STRING DELIMITED BY ALL SPACES
		INTO WS-FIELD(1)
		     WS-FIELD(2)
		     WS-FIELD(3)
		     WS-FIELD(4)
		     WS-FIELD(5)
		     WS-FIELD(6)
		     WS-FIELD(7)
		     WS-FIELD(8)
		TALLYING WS-FIELD-COUNT
	END-UNSTRING.
	
	CHECK-REQUIRED-FIELDS-PARA.
	MOVE 0 TO WS-PASSPORT-FIELDS-VALID.
	MOVE 0 TO WS-REQUIRED-COUNT.
	PERFORM VARYING J FROM 1 BY 1 UNTIL J>WS-FIELD-COUNT
		INSPECT WS-REQUIRED-FIELDS TALLYING WS-REQUIRED-COUNT FOR ALL WS-FIELD(J)(1:3)
	END-PERFORM.
	IF WS-REQUIRED-COUNT=7
		MOVE 1 TO WS-PASSPORT-FIELDS-VALID
	END-IF.
	
	VALIDATE-DATA-PARA.
	MOVE WS-FIELD(J) TO WS-CURRENT-FIELD.
	EVALUATE WS-CURRENT-KEY
		WHEN "byr"
			IF WS-CURRENT-VALUE<1920 OR WS-CURRENT-VALUE>2002
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "iyr"
			IF WS-CURRENT-VALUE<2010 OR WS-CURRENT-VALUE>2020
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "eyr"
			IF WS-CURRENT-VALUE<2020 OR WS-CURRENT-VALUE>2030
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "ecl"
			MOVE 0 TO WS-TALLY
			INSPECT WS-VALID-EYES TALLYING WS-TALLY FOR ALL WS-CURRENT-VALUE(1:3)
			IF NOT WS-TALLY=1
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "hcl"
			*> shortcut based on input analysis
			IF NOT WS-CURRENT-VALUE(1:1)="#"
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "pid"
			*> shortcut based on input analysis
			IF NOT FUNCTION LENGTH(FUNCTION TRIM(WS-CURRENT-VALUE))=9
				MOVE 0 TO WS-PASSPORT-DATA-VALID
			END-IF
		WHEN "hgt"
			IF WS-CURRENT-VALUE(4:2)="cm"
				IF WS-CURRENT-VALUE(1:3)<150 OR WS-CURRENT-VALUE(1:3)>193
					MOVE 0 TO WS-PASSPORT-DATA-VALID
				END-IF
			ELSE
				IF WS-CURRENT-VALUE(3:2)="in"
					IF WS-CURRENT-VALUE(1:2)<59 OR WS-CURRENT-VALUE(1:2)>76
						MOVE 0 TO WS-PASSPORT-DATA-VALID
					END-IF
				ELSE
					MOVE 0 TO WS-PASSPORT-DATA-VALID
				END-IF
			END-IF
	END-EVALUATE.
