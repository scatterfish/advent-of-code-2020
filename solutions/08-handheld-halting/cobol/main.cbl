IDENTIFICATION DIVISION.
	PROGRAM-ID. AOC-2020-DAY-08.

ENVIRONMENT DIVISION.
	INPUT-OUTPUT SECTION.
	FILE-CONTROL.
		SELECT InputFile ASSIGN TO "input.txt"
			ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
	FILE SECTION.
	FD InputFile.
	01 INPUT-FILE.
		05 INPUT-LINE PIC X(8).
	
	WORKING-STORAGE SECTION.
	01 WS-EOF PIC 9(1) VALUE 0.
	01 WS-INPUT.
		05 WS-INPUT-LINE PIC X(8).
	01 WS-INSTRUCTIONS.
		05 WS-INSTRUCTION OCCURS 700 TIMES INDEXED BY I.
			10 WS-OPCODE PIC A(3).
			10 WS-SPACE PIC A(1).
			10 WS-VALUE PIC X(4).
		05 WS-INSTRUCTIONS-SIZE PIC 9(4).
	01 WS-MACHINE-STATES.
		05 WS-IP PIC 9(8).
		05 WS-ACC PIC S9(8).
		05 WS-CURRENT-OPCODE PIC A(3).
		05 WS-CURRENT-VALUE PIC S9(3).
		05 WS-EDIT-IP PIC 9(8) VALUE 0.
		05 WS-TARGET-IP PIC 9(8).
		05 WS-KNOWN-IP-LIST OCCURS 700 TIMES INDEXED BY J.
			10 WS-IP-SEEN PIC 9(1).
		05 WS-HALT PIC 9(1).
	01 WS-FORMATTED-NUMBER PIC ZZZZZ.

PROCEDURE DIVISION.
	
	OPEN INPUT InputFile.
		PERFORM VARYING I FROM 1 BY 1 UNTIL WS-EOF=1
			READ InputFile INTO WS-INPUT-LINE
				AT END
					MOVE 1 TO WS-EOF
				NOT AT END
					MOVE WS-INPUT-LINE TO WS-INSTRUCTION(I)
					MOVE I TO WS-INSTRUCTIONS-SIZE
			END-READ
		END-PERFORM.
	CLOSE InputFile.
	
	PERFORM EXEC-INSTRUCTIONS-PARA.
	MOVE WS-ACC TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 1 answer: " WS-FORMATTED-NUMBER.
	
	MOVE WS-INSTRUCTIONS-SIZE TO WS-TARGET-IP.
	ADD 1 TO WS-TARGET-IP.
	PERFORM UNTIL WS-IP=WS-TARGET-IP
		ADD 1 TO WS-EDIT-IP
		PERFORM EXEC-INSTRUCTIONS-PARA
	END-PERFORM.
	MOVE WS-ACC TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 2 answer: " WS-FORMATTED-NUMBER.
	
	STOP RUN.
	
	EXEC-INSTRUCTIONS-PARA.
	MOVE 1 TO WS-IP.
	MOVE 0 TO WS-ACC.
	PERFORM VARYING J FROM 1 BY 1 UNTIL J>WS-INSTRUCTIONS-SIZE
		MOVE 0 TO WS-IP-SEEN(J)
	END-PERFORM.
	MOVE 0 TO WS-HALT.
	PERFORM UNTIL WS-HALT=1
		IF WS-IP-SEEN(WS-IP)=1 OR WS-IP>WS-INSTRUCTIONS-SIZE
			MOVE 1 TO WS-HALT
		ELSE
			MOVE 1 TO WS-IP-SEEN(WS-IP)
			MOVE WS-OPCODE(WS-IP) TO WS-CURRENT-OPCODE
			MOVE WS-VALUE(WS-IP) TO WS-CURRENT-VALUE
			IF WS-IP=WS-EDIT-IP
				EVALUATE WS-CURRENT-OPCODE
					WHEN "jmp"
						MOVE "nop" to WS-CURRENT-OPCODE
					WHEN "nop"
						MOVE "jmp" to WS-CURRENT-OPCODE
				END-EVALUATE
			END-IF
			EVALUATE WS-CURRENT-OPCODE
				WHEN "acc"
					ADD WS-CURRENT-VALUE TO WS-ACC
				WHEN "jmp"
					ADD WS-CURRENT-VALUE TO WS-IP
					SUBTRACT 1 FROM WS-IP
			END-EVALUATE
			ADD 1 TO WS-IP
		END-IF
	END-PERFORM.
