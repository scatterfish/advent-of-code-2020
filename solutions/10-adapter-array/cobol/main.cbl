IDENTIFICATION DIVISION.
	PROGRAM-ID. AOC-2020-DAY-10.

ENVIRONMENT DIVISION.
	INPUT-OUTPUT SECTION.
	FILE-CONTROL.
		SELECT InputFile ASSIGN TO "input.txt"
			ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
	FILE SECTION.
	FD InputFile.
	01 INPUT-FILE.
		05 INPUT-LINE PIC X(3).
	
	WORKING-STORAGE SECTION.
	01 WS-EOF PIC 9(1) VALUE 0.
	01 WS-INPUT.
		05 WS-INPUT-LINE PIC X(3).
	01 WS-JOLTAGES.
		05 WS-RATING OCCURS 120 TIMES INDEXED BY I.
			10 WS-JOLT PIC 9(3).
		05 WS-JOLT-MAX PIC 9(3) VALUE 0.
		05 WS-RATING-COUNT PIC 9(4).
	01 WS-DELTAS.
		05 WS-DELTA PIC 9(4).
		05 WS-DELTA-1-COUNT PIC 9(4) VALUE 0.
		05 WS-DELTA-3-COUNT PIC 9(4) VALUE 0.
		05 WS-DELTA-PRODUCT PIC 9(8).
	01 WS-PATHS.
		05 WS-PATH-COUNT OCCURS 120 TIMES INDEXED BY J.
			10 WS-COUNT PIC 9(20) VALUE 0.
	01 WS-FORMATTED-NUMBER PIC Z(14).

PROCEDURE DIVISION.
	
	OPEN INPUT InputFile.
		PERFORM VARYING I FROM 3 BY 1 UNTIL WS-EOF=1
			READ InputFile INTO WS-INPUT-LINE
				AT END
					MOVE 1 TO WS-EOF
				NOT AT END
					MOVE WS-INPUT-LINE TO WS-JOLT(I)
					IF WS-JOLT(I)>WS-JOLT-MAX
						MOVE WS-JOLT(I) TO WS-JOLT-MAX
					END-IF
					MOVE I TO WS-RATING-COUNT
			END-READ
		END-PERFORM.
	CLOSE InputFile.
	
	MOVE 0 TO WS-JOLT(1).
	ADD 3 TO WS-JOLT-MAX.
	MOVE WS-JOLT-MAX TO WS-JOLT(2).
	SORT WS-RATING DESCENDING KEY WS-JOLT.
	
	PERFORM VARYING I FROM 2 BY 1 UNTIL I>WS-RATING-COUNT
		SET J TO I
		SET J DOWN BY 1
		COMPUTE WS-DELTA=(WS-JOLT(I))-(WS-JOLT(J))
		IF WS-DELTA=1
			ADD 1 TO WS-DELTA-1-COUNT
		ELSE
			ADD 1 TO WS-DELTA-3-COUNT
		END-IF
	END-PERFORM.
	
	MULTIPLY WS-DELTA-1-COUNT BY WS-DELTA-3-COUNT GIVING WS-DELTA-PRODUCT.
	MOVE WS-DELTA-PRODUCT TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 1 answer: " WS-FORMATTED-NUMBER.
	
	MOVE 1 TO WS-COUNT(1).
	PERFORM VARYING I FROM 1 BY 1 UNTIL I>WS-RATING-COUNT
		PERFORM VARYING J FROM 1 BY 1 UNTIL J=I
			COMPUTE WS-DELTA=(WS-JOLT(I))-(WS-JOLT(J))
			IF WS-DELTA<=3
				ADD WS-COUNT(J) TO WS-COUNT(I)
			END-IF
		END-PERFORM
	END-PERFORM.
	
	MOVE WS-COUNT(WS-RATING-COUNT) TO WS-FORMATTED-NUMBER.
	DISPLAY "Part 2 answer: " WS-FORMATTED-NUMBER.
	
STOP RUN.
